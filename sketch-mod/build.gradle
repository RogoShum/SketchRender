buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id + "-" + mapping_version
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                    // Include sketch-core sources in the run config so it can be debugged/viewed
                    source project(':sketch-core').sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
    debug.verbose = true
    debug.export = true
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Dependencies
    compileOnly fg.deobf('maven.modrinth:oculus:1.20.1-1.8.0')
    implementation fg.deobf('maven.modrinth:oculus:1.20.1-1.8.0')

    compileOnly fg.deobf("maven.modrinth:embeddium:0.3.31+mc1.20.1")
    implementation fg.deobf("maven.modrinth:embeddium:0.3.31+mc1.20.1")

    // spark
    compileOnly fg.deobf("maven.modrinth:l6YH9Als:4FXHDE9F")
    implementation fg.deobf("maven.modrinth:l6YH9Als:4FXHDE9F")

    // JOML primitives for spatial data structures
    minecraftLibrary ("org.joml:joml-primitives:1.10.0")
    implementation(jarJar("org.joml:joml-primitives:1.10.0")) {
        jarJar.ranged(it, "[1.10.0,)")
    }

    // Project dependencies
    implementation project(':sketch-core')
    
    // JarJar sketch-core so it's included in the final mod jar
    jarJar(project(':sketch-core')) {
         jarJar.ranged(it, "[${mod_version},)")
    }
}

// Ensure resources are processed correctly
tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
                             mod_authors         : mod_authors, mod_description: mod_description,]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }
    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.register("runRenderDoc", Exec.class) {
    group = "forgegradle runs"

    var javaExecTask = tasks.withType(JavaExec.class).named("runClient").get()
    var javaHome = javaExecTask.javaLauncher.get().metadata.installationPath.asFile.absolutePath

    var rootProject = project.rootProject
    var wrapperJar = rootProject.file("gradle/wrapper/gradle-wrapper.jar")

    if (!wrapperJar.exists()) {
        throw new GradleException("can't find gradle-wrapper.jar: ${wrapperJar.absolutePath}")
    }

    commandLine "C:\\Program Files\\RenderDoc\\renderdoccmd.exe",
            "capture",
            "--opt-hook-children",
            "--opt-api-validation",
            "--opt-verify-buffer-access",
            "--wait-for-exit",
            "--working-dir",
            rootProject.projectDir.absolutePath,
            "$javaHome/bin/java.exe",
            "-Xmx64m",
            "-Xms64m",
            "-Dfile.encoding=UTF-8",
            "-Dorg.gradle.appname=gradlew",
            "-Dorg.gradle.java.home=$javaHome",
            "-classpath",
            wrapperJar.absolutePath,
            "org.gradle.wrapper.GradleWrapperMain",
            "runClient"
}

gradle.taskGraph.whenReady {
    tasks.named("runClient", JavaExec).configure {
        jvmArgs += [
                "-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005"
        ]
    }
}