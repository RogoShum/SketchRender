#version 430

layout(binding = 0) uniform writeonly image2D output_texture;

uniform sampler2D sketch_sampler_texture;
uniform ivec2 sketch_screen_size;
uniform ivec2 sketch_depth_size;

#define TILE_SIZE_X 16
#define TILE_SIZE_Y 16
#define TILE_EDGE_X TILE_SIZE_X * 2
#define TILE_EDGE_Y TILE_SIZE_Y * 2

layout(local_size_x = TILE_SIZE_X, local_size_y = TILE_SIZE_Y) in;

shared float depthCache[TILE_EDGE_X][TILE_EDGE_Y];

void main() {
    ivec2 groupStart = ivec2(gl_WorkGroupID.xy * gl_WorkGroupSize.xy);
    ivec2 depthCoords = groupStart + ivec2(gl_LocalInvocationID.xy);

    bool validPixel = true;

    if(depthCoords.x >= sketch_depth_size.x ||
    depthCoords.y >= sketch_depth_size.y) {
        validPixel = false;
    }

    if (validPixel) {
        ivec2 srcPos = depthCoords * 2;

        for(int y = 0; y < 2; y++) {
            for(int x = 0; x < 2; x++) {
                ivec2 loadPos = srcPos + ivec2(x, y);
                if(loadPos.x < sketch_screen_size.x &&
                loadPos.y < sketch_screen_size.y) {
                    depthCache[gl_LocalInvocationID.y * 2 + y]
                    [gl_LocalInvocationID.x * 2 + x] =
                    texelFetch(sketch_sampler_texture, loadPos, 0).r;
                }
            }
        }
    }

    barrier();

    if (validPixel) {
        float maxDepth = depthCache[gl_LocalInvocationID.y * 2]
            [gl_LocalInvocationID.x * 2];

        maxDepth = max(maxDepth,
            depthCache[gl_LocalInvocationID.y * 2]
            [gl_LocalInvocationID.x * 2 + 1]);

        maxDepth = max(maxDepth,
            depthCache[gl_LocalInvocationID.y * 2 + 1]
            [gl_LocalInvocationID.x * 2]);

        maxDepth = max(maxDepth,
            depthCache[gl_LocalInvocationID.y * 2 + 1]
            [gl_LocalInvocationID.x * 2 + 1]);

        imageStore(output_texture, depthCoords, vec4(maxDepth));
    }
}