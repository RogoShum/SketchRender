#version 430

layout(binding = 0, r16f) uniform image2D output_texture_0;
layout(binding = 1, r16f) uniform image2D output_texture_1;
layout(binding = 2, r16f) uniform image2D output_texture_2;
layout(binding = 3, r16f) uniform image2D output_texture_3;
layout(binding = 4, r16f) uniform image2D output_texture_4;
layout(binding = 5, r16f) uniform image2D output_texture_5;

uniform sampler2D sketch_sampler_texture_0;
uniform ivec2 sketch_screen_size;
uniform ivec2 sketch_depth_size;
uniform int sketch_render_distance;

#define MIP_LEVELS 6
#define TILE_SIZE_X 16
#define TILE_SIZE_Y 16
#define TILE_EDGE_X TILE_SIZE_X * 2
#define TILE_EDGE_Y TILE_SIZE_Y * 2

layout(local_size_x = TILE_SIZE_X, local_size_y = TILE_SIZE_Y) in;

shared float depthCache[TILE_EDGE_X][TILE_EDGE_Y];

float near = 0.05;
float far  = 16.0;

float LinearizeDepth(float depth) {
    float z = depth * 2.0 - 1.0;
    return (near * far) / (far + near - z * (far - near));
}

void processLevel(int level, ivec2 depthCoords) {
    ivec2 currentSize = sketch_depth_size >> level;
    bool validPixel = depthCoords.x < currentSize.x &&
    depthCoords.y < currentSize.y;

    if (validPixel) {
        ivec2 srcPos = depthCoords * 2;
        for(int y = 0; y < 2; y++) {
            for(int x = 0; x < 2; x++) {
                ivec2 loadPos = srcPos + ivec2(x, y);
                if(loadPos.x < (currentSize.x * 2) &&
                loadPos.y < (currentSize.y * 2)) {
                    float depth = 0.0;
                    switch(level) {
                        case 0: depth = LinearizeDepth(texelFetch(sketch_sampler_texture_0, loadPos, 0).r); break;
                        case 1: depth = imageLoad(output_texture_0, loadPos).r; break;
                        case 2: depth = imageLoad(output_texture_1, loadPos).r; break;
                        case 3: depth = imageLoad(output_texture_2, loadPos).r; break;
                        case 4: depth = imageLoad(output_texture_3, loadPos).r; break;
                        case 5: depth = imageLoad(output_texture_4, loadPos).r; break;
                    }
                    depthCache[gl_LocalInvocationID.y * 2 + y]
                    [gl_LocalInvocationID.x * 2 + x] = depth;
                }
            }
        }

        barrier();

        float maxDepth = depthCache[gl_LocalInvocationID.y * 2]
            [gl_LocalInvocationID.x * 2];

        maxDepth = max(maxDepth,
            depthCache[gl_LocalInvocationID.y * 2]
            [gl_LocalInvocationID.x * 2 + 1]);

        maxDepth = max(maxDepth,
            depthCache[gl_LocalInvocationID.y * 2 + 1]
            [gl_LocalInvocationID.x * 2]);

        maxDepth = max(maxDepth,
            depthCache[gl_LocalInvocationID.y * 2 + 1]
            [gl_LocalInvocationID.x * 2 + 1]);

        switch(level) {
            case 0: imageStore(output_texture_0, depthCoords, vec4(maxDepth)); break;
            case 1: imageStore(output_texture_1, depthCoords, vec4(maxDepth)); break;
            case 2: imageStore(output_texture_2, depthCoords, vec4(maxDepth)); break;
            case 3: imageStore(output_texture_3, depthCoords, vec4(maxDepth)); break;
            case 4: imageStore(output_texture_4, depthCoords, vec4(maxDepth)); break;
            case 5: imageStore(output_texture_5, depthCoords, vec4(maxDepth)); break;
        }
    }

    barrier();
}

void main() {
    ivec2 groupStart = ivec2(gl_WorkGroupID.xy * gl_WorkGroupSize.xy);
    ivec2 depthCoords = groupStart + ivec2(gl_LocalInvocationID.xy);
    far = sketch_render_distance * 64.0;

    for(int level = 0; level < MIP_LEVELS; level++) {
        processLevel(level, depthCoords);
    }
}